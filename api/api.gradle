plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  id 'maven'
}


ext {
  vertxVersion = '3.8.4'
}

sourceCompatibility = '1.8'

// https://docs.gradle.org/5.2.1/userguide/java_library_plugin.html
// compileOnly = only for the compilation not for the test
dependencies {

  // Vert.x
  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-config:$vertxVersion" // Config management: https://vertx.io/docs/vertx-config/java/
  // Web
  compile "io.vertx:vertx-web:$vertxVersion"
  compile "io.vertx:vertx-web-client:$vertxVersion"
  compile "io.vertx:vertx-web-api-contract:$vertxVersion" // Used to verify that the request has the good structure
  // Database
  compile "io.vertx:vertx-jdbc-client:$vertxVersion"
  compile "org.hsqldb:hsqldb:2.5.0"
  //implementation "org.xerial:sqlite-jdbc:3.28.0"
  compile "org.flywaydb:flyway-core:6.1.1"
  compile project(':bytle-db')
  compile project(':bytle-db-jdbc')
  compile project(':bytle-db-fs')
  // File system
  compile project(':bytle-http')
  compile project(':bytle-zip')
  // Vertx service
  compile "io.vertx:vertx-service-proxy:$vertxVersion"
  compileOnly "io.vertx:vertx-codegen:$vertxVersion"
  annotationProcessor "io.vertx:vertx-codegen:$vertxVersion:processor"
  // <3.8.4
  // annotationProcessor "io.vertx:vertx-service-proxy:$vertxVersion:processor"
  annotationProcessor "io.vertx:vertx-service-proxy:$vertxVersion"
  // for the service health
  compile "io.vertx:vertx-health-check:$vertxVersion"
  compile "io.vertx:vertx-dropwizard-metrics:$vertxVersion"
  compile "io.vertx:vertx-circuit-breaker:$vertxVersion"
  // Test
  testImplementation "io.vertx:vertx-unit:$vertxVersion"

}

def mainClassName = 'net.bytle.api.Launcher'
def watchForChange = 'src/main/java/net/**/*'
def doOnChange = './gradlew classes'
def mainVerticle = 'net.bytle.api.MainVerticle'

shadowJar {
  manifest {
    attributes 'Main-Class': mainClassName
    attributes 'Main-Verticle': mainVerticle
    attributes 'Main-Command': 'run'
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

// Is a dependence on compileJava
// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.compile.JavaCompile.html
// the generated code is put in src/main/generated,
// which some integrated development environments like IntelliJ IDEA will automatically pick up on the classpath.
// https://github.com/bulivlad/vertx-codegen-plugin
// https://gitter.im/eclipse-vertx/vertx-users?at=5d8db754bf625112c0eb65e4
task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
  description 'Generates the stubs'
  options.incremental = false
  source = sourceSets.main.java
  classpath = configurations.compileClasspath
  destinationDir = project.file('src/main/generated')
  options.annotationProcessorPath = configurations.annotationProcessor
  options.debugOptions.debugLevel = "source,lines,vars"
  options.compilerArgs = [
    "-proc:only",
    "-processor", "io.vertx.codegen.CodeGenProcessor",
    "-Acodegen.output=${project.projectDir}/src/main"
  ]
}

// Adding the generated map to the set of source directory
sourceSets {
  main {
    java {
      srcDirs += 'src/main/generated'
    }
  }
}


compileJava {
  targetCompatibility = 1.8
  sourceCompatibility = 1.8
  dependsOn(annotationProcessing)
}

compileTestJava {
  options.compilerArgs += '-proc:none'
}


// Run args from the application id
// https://vertx.io/blog/automatic-redeployment-in-eclipse-ide/
// Same as: vertx run  --redeploy=src/**/* --launcher-class=net.bytle.api.Launcher --on-redeploy="./gradlew classes"
//run {
//  args = ['run', "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
//}

uploadShadow {
  repositories {
    mavenDeployer {
      repository(
        url: "${nexusUrl}/repository/maven-releases/") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }
      snapshotRepository(
        url: "${nexusUrl}/repository/maven-snapshots/") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "${nexusUrl}/repository/maven-releases/") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }
      snapshotRepository(url: "${nexusUrl}/repository/maven-snapshots/") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }
    }
  }
}
