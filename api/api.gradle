plugins {
  id 'java'
  id 'java-library' // for the api
  id 'application' // https://docs.gradle.org/current/userguide/application_plugin.html
}

repositories {
  mavenCentral()
}


ext {
  vertxVersion = '3.8.4' // 4.0.0-milestone4
}

configurations {
  vertx
}


sourceCompatibility = '1.8'

// https://docs.gradle.org/5.2.1/userguide/java_library_plugin.html
// compileOnly = only for the compilation not for the test
dependencies {

  // Vert.x
  api   "io.vertx:vertx-core:$vertxVersion"
  api  "io.vertx:vertx-config:$vertxVersion" // Config management: https://vertx.io/docs/vertx-config/java/
  // Web
  api  "io.vertx:vertx-web:$vertxVersion"
  api  "io.vertx:vertx-web-client:$vertxVersion"
  api  "io.vertx:vertx-web-api-contract:$vertxVersion" // Used to verify that the request has the good structure
  // Database
  api  "io.vertx:vertx-jdbc-client:$vertxVersion"
  implementation "org.hsqldb:hsqldb:2.5.0"
  //implementation "org.xerial:sqlite-jdbc:3.28.0"
  implementation "org.flywaydb:flyway-core:6.1.1"
  implementation project(':bytle-db')
  implementation project(':bytle-db-jdbc')
  implementation project(':bytle-db-fs')
  // File system
  implementation project(':bytle-http')
  implementation project(':bytle-zip')
  // Vertx service
  api  "io.vertx:vertx-service-proxy:$vertxVersion"
  compileOnly  "io.vertx:vertx-codegen:$vertxVersion"
  annotationProcessor "io.vertx:vertx-codegen:$vertxVersion:processor"
  // <3.8.4
  // annotationProcessor "io.vertx:vertx-service-proxy:$vertxVersion:processor"
  annotationProcessor "io.vertx:vertx-service-proxy:$vertxVersion"
  // for the service health
  api  "io.vertx:vertx-health-check:$vertxVersion"
  api  "io.vertx:vertx-dropwizard-metrics:$vertxVersion"
  api  "io.vertx:vertx-circuit-breaker:$vertxVersion"
  // Test
  testImplementation "io.vertx:vertx-unit:$vertxVersion"

}

mainClassName = 'net.bytle.api.Main'
def watchForChange = 'src/main/java/net/**/*'
def doOnChange = './gradlew classes'

// Is a dependence on compileJava
// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.compile.JavaCompile.html
// the generated code is put in src/main/generated,
// which some integrated development environments like IntelliJ IDEA will automatically pick up on the classpath.
// https://github.com/bulivlad/vertx-codegen-plugin
// https://gitter.im/eclipse-vertx/vertx-users?at=5d8db754bf625112c0eb65e4
task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
  description 'Generates the stubs'
  options.incremental = false
  source = sourceSets.main.java
  classpath = configurations.compileClasspath
  destinationDir = project.file('src/main/generated')
  options.annotationProcessorPath = configurations.annotationProcessor
  options.debugOptions.debugLevel = "source,lines,vars"
  options.compilerArgs = [
    "-proc:only",
    "-processor", "io.vertx.codegen.CodeGenProcessor",
    "-Acodegen.output=${project.projectDir}/src/main"
  ]
}

// Adding the generated map to the set of source directory
sourceSets {
  main {
    java {
      srcDirs += 'src/main/generated'
    }
  }
}


compileJava {
  targetCompatibility = 1.8
  sourceCompatibility = 1.8
  dependsOn (annotationProcessing)
}

compileTestJava {
  options.compilerArgs += '-proc:none'
}


test {
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}



// Run args from the application id
// https://vertx.io/blog/automatic-redeployment-in-eclipse-ide/
// Same as: vertx run  --redeploy=src/**/* --launcher-class=net.bytle.api.Main --on-redeploy="./gradlew classes"
run {
  args = ['run', "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

